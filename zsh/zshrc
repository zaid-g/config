#



# %% ---------- [vi mode] ----------:

bindkey -v
export KEYTIMEOUT=1


#  %% -------- [bindings] ----------:

# Bind Ctrl-space to clear-screen
bindkey '^@' clear-screen

# %% ---------- [colors and prompt] ----------:

autoload -U colors && colors
PS1=" %{$fg[green]%}%~%{$fg[green]%} %{$reset_color%}$%b "
zle_highlight=('region:bg=168,fg=251')
zle_highlight=('paste:bg=168,fg=251', 'region:bg=168,fg=251')

# %% ---------- [auto-completion] ----------:

autoload -U compinit
zmodload zsh/complist
compinit
_comp_options+=(globdots)              # Include hidden files.
zstyle ':completion:*' menu select
bindkey -M menuselect '^[[Z' reverse-menu-complete

# 0 -- vanilla completion (abc => abc)
# 1 -- smart case completion (abc => Abc)
# 2 -- word flex completion (abc => A-big-Car)
# 3 -- full flex completion (abc => ABraCadabra)
zstyle ':completion:*' matcher-list '' \
    'm:{a-z\-}={A-Z\_}' \
    'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
    'r:|?=** m:{a-z\-}={A-Z\_}'


# %% -------- [vi vim lazyvim neovim nvim] ----------:


export NEOVIM_EDITOR=~/app/neovim/squashfs-root/AppRun
export EDITOR=~/app/neovim/squashfs-root/AppRun
# vim, VM taken
alias V='$NEOVIM_EDITOR'
VP() {
    local files
    if [ "$#" -eq 0 ]; then
        files=($(get_allowed_files_matching_types))  # All allowed files
    else
        files=($(get_allowed_files_matching_types "$@"))  # Filtered by extensions
    fi
    if (( ${#files[@]} )); then
        $NEOVIM_EDITOR "${files[@]}"
    else
        echo "No matching files found outside forbidden folders."
    fi
}
VB() {
    bemol --verbose --watch > /tmp/bemol.log 2>&1 &
    local bemol_pid=$!
    $NEOVIM_EDITOR "$@"
    kill $bemol_pid 2>/dev/null
}
VS(){
    git_branch=$(git branch --show-current)
    $NEOVIM_EDITOR -S .vim/$git_branch.vim
}
alias VCHEAT='$NEOVIM_EDITOR ~/doc/cheatsheet.txt'
alias VSCRATCH='$NEOVIM_EDITOR ~/doc/scratch.txt'
alias VPROMPTS='$NEOVIM_EDITOR ~/doc/prompts.txt'
alias VZ='$NEOVIM_EDITOR ~/.zshrc'

# %% -------- [grep] ----------:

export GNU_GREP=$(which grep)
GREP() {
    local pattern
    local files

    if [ "$#" -eq 1 ]; then
        pattern="$1"
        files=$(get_allowed_files_matching_types)
    else
        pattern="${@: -1}"
        local exts=("${@:1:$#-1}")
        files=$(get_allowed_files_matching_types "${exts[@]}")
    fi

    [ -z "$files" ] && return

    while IFS= read -r file; do
        "$GNU_GREP" -Hni -e "$pattern" "$file"
    done <<< "$files"
}


# %% -------- [find] ----------:

export GNU_FIND=$(which find)
FORBIDDEN_FOLDER_PATTERNS=("node_modules" "build" ".*" "bin" "dist")

get_all_allowed_files() {
    local forbidden=("${FORBIDDEN_FOLDER_PATTERNS[@]}")
    local find_cmd=(find .)
    for pattern in "${forbidden[@]}"; do
        find_cmd+=(-path "*/$pattern" -prune -o)
    done
    find_cmd+=(-type f -print)
    "${find_cmd[@]}"
}
# Function to find files matching given extensions
get_allowed_files_matching_types() {
    local extensions=("$@")
    local files
    files=$(get_all_allowed_files)
    # If extensions are given, filter by them (case-insensitive)
    if [ "${#extensions[@]}" -gt 0 ]; then
        local pattern
        pattern=$(IFS=\|; echo "${extensions[*]}" | sed 's/ /|/gI')
        echo "$files" | grep -Eiq "\.(${pattern})$" && echo "$files" | grep -Ei "\.(${pattern})$" | sort || return
    else
        echo "$files" | sort
    fi
}
# Function to find files matching a general pattern
get_files_matching_pattern() {
    local pattern="$1"
    local files
    files=$(get_all_allowed_files)
    # If a pattern is given, filter by it (case-insensitive)
    if [ -n "$pattern" ]; then
        echo "$files" | grep -Eiq "$pattern" && echo "$files" | grep -Ei "$pattern" | sort || return
    else
        echo "$files" | sort
    fi
}
# Shorthand functions
FINDT() {
    get_allowed_files_matching_types "$@"
}
FIND() {
    get_files_matching_pattern "$@"
}



# %% -------- [cd and directories] ----------:

cd() {
    builtin cd $1
    current_working_directory_leaf="$(basename $(pwd))"
    if [[ -d ~/.virtualenvs/.$current_working_directory_leaf ]] ; then
        if [[ -z "$VIRTUAL_ENV" ]] ; then
            . ~/.virtualenvs/.$current_working_directory_leaf/bin/activate
        fi
        deactivate
        . ~/.virtualenvs/.$current_working_directory_leaf/bin/activate
    fi
}

alias CDC="cd ~/doc/config"
alias CDA="cd ~/doc"
alias CDD="cd ~/Downloads"
alias CD="cd ~/doc"
alias CDT="cd ~/trash"
MKT() {
    local base_dir=~/trash
    local name=${1:-$(LC_ALL=C tr -dc a-z < /dev/urandom | head -c 8)}
    local dir="$base_dir/${name}_$(date +%F_%H-%M-%S)"
    mkdir -p "$dir" && cd "$dir"
}
MK() {
    local name=${1:-$(LC_ALL=C tr -dc a-z < /dev/urandom | head -c 8)}
    local dir="${name}_$(date +%F_%H-%M-%S)"
    mkdir -p "$dir" && cd "$dir"
}
MKD() {
    mkdir -p ~/doc/"$1" && cd ~/doc/"$1"
}

alias mv="mv -v"

_GET_FROM_DOWNLOADS(){
    if [[ "$1" =~ ^[0-9]+$ ]]; then
        # If argument is a number, get that many recent files
        ls -Art ~/Downloads | tail -n $1 | while read file; do echo ~/Downloads/"$file"; done
    else
        # No arguments or invalid, get latest file
        echo ~/Downloads/$(ls -Art ~/Downloads | tail -n 1)
    fi
}

MV(){
    if [[ "$1" =~ ^[0-9]+$ ]] && [ "$#" -eq 2 ]; then
        # Number and prefix - move N files with prefix
        _GET_FROM_DOWNLOADS "$1" | while read file; do
            filename=$(basename "$file")
            mv "$file" "${2}_$filename"
        done
    elif [[ "$1" =~ ^[0-9]+$ ]] || [ "$#" -eq 0 ]; then
        # Number or no args - move files
        _GET_FROM_DOWNLOADS "$1" | while read file; do
            mv "$file" .
        done
    else
        # Non-numeric arg - use as prefix for latest file
        latest_download=~/Downloads/$(ls -Art ~/Downloads | tail -n 1)
        latest_download_file_name=$(basename "$latest_download")
        mv "$latest_download" "${1}_$latest_download_file_name"
    fi
}

CP(){
    if [[ "$1" =~ ^[0-9]+$ ]] && [ "$#" -eq 2 ]; then
        # Number and prefix - copy N files with prefix
        _GET_FROM_DOWNLOADS "$1" | while read file; do
            filename=$(basename "$file")
            cp "$file" "${2}_$filename"
        done
    elif [[ "$1" =~ ^[0-9]+$ ]] || [ "$#" -eq 0 ]; then
        # Number or no args - copy files
        _GET_FROM_DOWNLOADS "$1" | while read file; do
            cp "$file" .
        done
    else
        # Non-numeric arg - use as prefix for latest file
        latest_download=~/Downloads/$(ls -Art ~/Downloads | tail -n 1)
        latest_download_file_name=$(basename "$latest_download")
        cp "$latest_download" "${1}_$latest_download_file_name"
    fi
}

RM(){
    _GET_FROM_DOWNLOADS "$1" | while read file; do
        [ -f "$file" ] && rm "$file"
    done
}

CPR(){
    ssh $REMOTE "mkdir -p ~/Downloads"
    if [[ "$1" =~ ^[0-9]+$ ]] && [ "$#" -eq 2 ]; then
        # Number and prefix - copy N files with prefix
        _GET_FROM_DOWNLOADS "$1" | while read file; do
            filename=$(basename "$file")
            [ -f "$file" ] && scp "$file" $REMOTE:~/Downloads/"${2}_$filename"
        done
    elif [[ "$1" =~ ^[0-9]+$ ]] || [ "$#" -eq 0 ]; then
        # Number or no args - copy files
        _GET_FROM_DOWNLOADS "$1" | while read file; do
            [ -f "$file" ] && scp "$file" $REMOTE:~/Downloads/
        done
    else
        # Non-numeric arg - use as prefix for latest file
        latest_download=~/Downloads/$(ls -Art ~/Downloads | tail -n 1)
        latest_download_file_name=$(basename "$latest_download")
        scp "$latest_download" $REMOTE:~/Downloads/"${1}_$latest_download_file_name"
    fi
}

SCPPULL() {
    local USER_HOST=$1
    local REMOTE_PATH=$2
    local LOCAL_DIR=$3
    if [ -z "$USER_HOST" ] || [ -z "$REMOTE_PATH" ] || [ -z "$LOCAL_DIR" ]; then
        echo "Usage: SCP <user@host> <remote_path> <local_dir>"
        return 1
    fi
    mkdir -p "$LOCAL_DIR"
    # Strip leading and trailing slashes, then replace remaining slashes with underscores
    local CLEAN_PATH=$(echo "$REMOTE_PATH" | sed 's|^/||; s|/$||; s|/|_|g')
    local LOCAL_NAME="${USER_HOST}_${CLEAN_PATH}"
    if scp -r $USER_HOST:$REMOTE_PATH "$LOCAL_DIR/$LOCAL_NAME"; then
        chmod -R u+w "$LOCAL_DIR/"
        echo "✅ Copied to: $LOCAL_DIR/$LOCAL_NAME"
    else
        echo "❌ SCP failed for $USER_HOST:$REMOTE_PATH"
        return 1
    fi
}



# %% -------- [config] ----------:

alias AC="bash ~/doc/config/apply_config.sh"
alias FAAC="bash ~/doc/config/fetch_and_apply_config.sh"


# %% -------- [ls] ----------:

alias ls="ls --color=auto"
alias LL="ls -lt --color=auto"
alias LLR="ls -ltr --color=auto"
alias LLALL="ls -lA --color=auto"
alias LLA="ls -l --color=auto"


# %% -------- [python] ----------:

alias I="python3 -m IPython"
alias P="python3"
alias PT="python3 -m pytest -sxvv"
alias PTDB="pytest -sxvv --pdb"
alias BLACK="python3 -m black **/*.py"
alias PC="pre-commit run -a"

PIP3(){
    pip3 install wheel
    pip3 install ipdb==0.13.11
    pip3 install ipython==7.31.1
    pip3 install neovim
    pip3 install pandas
    pip3 install black
}

PVENV(){
    current_working_directory=$(pwd)
    project_name="$(basename $(pwd))"
    deactivate
    if [ -d ~/.virtualenvs/.$project_name ]; then
        rm -rf ~/.virtualenvs/.$project_name
    fi
    mkdir -p .vim
    mkdir -p ~/.virtualenvs
    builtin cd ~/.virtualenvs
    if [ "$#" -eq 0 ]; then
        python3 -m venv ".$project_name"
    else
        $1 -m venv ".$project_name"
    fi
    . ./".$project_name"/bin/activate
    builtin cd $current_working_directory
    touch requirements.txt
    touch Dockerfile
    if ! [ -e .git ]; then
        git init
        wget https://raw.githubusercontent.com/github/gitignore/main/Python.gitignore
        wget https://raw.githubusercontent.com/pre-commit/pre-commit/main/.pre-commit-config.yaml
        mv Python.gitignore .gitignore
        echo "\n# vim\n.vim\n.ccls*" >> .gitignore
    fi;
    PIP3 # keep at end
}


# %% -------- [tmux] ----------:

alias T="tmux -2"
alias TLS="tmux ls"
alias TAS="tmux attach-session -t"
alias TKS="tmux kill-session -t"
alias TKAS="tmux kill-server"

TS() {
    tmux new-session -d
    SESSION=$(tmux display-message -p '#S')

    # "doc" (horizontal split)
    tmux rename-window -t $SESSION:0 'doc'
    tmux split-window -h -t $SESSION:0
    tmux send-keys -t $SESSION:0.0 'CD' C-m
    tmux send-keys -t $SESSION:0.1 'VCHEAT' C-m

    # "scratch" (horizontal split)
    tmux new-window -t $SESSION: -n 'scratch'
    tmux split-window -h -t $SESSION:1
    tmux send-keys -t $SESSION:1.0 'CDT' C-m
    tmux send-keys -t $SESSION:1.1 'VSCRATCH' C-m

    # "ai" (horizontal split)
    tmux new-window -t $SESSION: -n 'AI'
    tmux split-window -h -t $SESSION:2
    tmux send-keys -t $SESSION:2.0 'QQ' C-m
    tmux send-keys -t $SESSION:2.1 'VPROMPTS' C-m

    # "config" (horizontal split)
    tmux new-window -t $SESSION: -n 'config'
    tmux split-window -h -t $SESSION:3
    tmux send-keys -t $SESSION:3.0 'CDC' C-m
    tmux send-keys -t $SESSION:3.1 'CDC' C-m

    tmux attach-session -t $SESSION:0
}


# %% -------- [git] ----------:

alias G="git"
alias GS="git status --untracked-files=all"
alias GI="git init"
alias GR="git restore"
alias GD="git diff"
alias GA="git add"
alias GC="git commit"
alias GCM="git commit -m"
alias GCH="git checkout"
alias GM="git merge"
alias GF="git fetch"
alias GL="git log --graph --decorate --oneline"
alias GFO="git fetch origin"
alias GB="git branch --sort=-committerdate"
alias GPUSH="git push -u origin HEAD"
alias GPULL='git pull --rebase origin $(git rev-parse --abbrev-ref HEAD)'
function GDIFF() {
    git rev-parse --git-dir >/dev/null 2>&1 || { echo "Not a git repo"; return 1; }
    local tmpfile
    tmpfile=$(mktemp)
    git diff "$@" >> "$tmpfile"
    if [[ $# -eq 0 ]]; then
        git ls-files --others --exclude-standard | while read -r file; do
            git diff --no-index /dev/null "$file" >> "$tmpfile"
        done
    fi
    cat "$tmpfile" | delta --side-by-side --line-numbers
    rm "$tmpfile"
}
function GSQUASH() {
    if [[ $# -ne 2 ]]; then
        echo "Usage: GSQUASH <commit_hash> <commit_message>"
        return 1
    fi
    local commit_hash="$1"
    local commit_message="$2"
    if ! git rev-parse --verify "$commit_hash" >/dev/null 2>&1; then
        echo "Error: Invalid commit hash"
        return 1
    fi
    git reset --soft "$commit_hash" && git commit -m "$commit_message"
}
GSTASH() {
    local msg="${*:-"WIP stash"}"
    git stash push -u -m "$msg"
}

# %% -------- [docker] ----------:

DOCK(){
    sudo docker run -it -v $HOME/doc/config:/home/ubuntu/doc/config $@ main
}
DOCKCWD(){
    sudo docker run -it -v $(pwd):$(pwd) -v $HOME/doc/config:/home/ubuntu/doc/config -w $(pwd) $@ main
}
alias D="docker"
alias DB="docker build"
alias DR="docker run"
alias DC="docker container"
alias DCE="docker container exec"
alias DI="docker image"


#  %% -------- [brazil] ----------:

alias BB=brazil-build
alias BRC='brazil-recursive-cmd'
alias BRCAP="brazil ws clean && brazil-recursive-cmd --allPackages brazil-build"
alias BWS="brazil ws --sync --md"
alias BW='brazil ws'
alias BWU='brazil ws use -p'
alias BWR='brazil ws remove -p'
alias BWC='brazil ws create --root'
alias BBCR='brazil ws clean && brazil-build clean && brazil-build release'
alias BBC='brazil-build clean'
alias BWC='brazil ws clean'
alias BBR='brazil-build release'
alias BBDEPLOY="brazil-build cdk deploy --require-approval never"


#  %% -------- [AI] ----------:

alias QQ="q chat --trust-all-tools"
alias QQE="cd ~/empty && q chat --trust-all-tools"
alias WW="cd /Users/zghar/doc/whisper-dictation && P src/main.py"

#  %% -------- [browsers chrome firefox] ----------:

alias FF="firefox"
alias CHROME="google-chrome-stable --ozone-platform=wayland"

#  %% -------- [sleep caff decaff] ----------:

alias CAFF='caffeinate -dimsu &'
alias DECAFF='pkill caffeinate && echo "Killed caffeinate process" || echo "No caffeinate process found"'
alias LIDCAFF='sudo pmset -a disablesleep 1'
alias LIDDECAFF='sudo pmset -a disablesleep 0'

# %% -------- [others] ----------:

alias S="sudo"
alias FP='readlink -f'
alias RS="source ~/.zshrc && cd ."
alias VM="virt-manager"
alias PO="systemctl poweroff"
# xdg open (open file from terminal with default app)
alias X="xdg-open"
alias DISCORD="snap run discord"
alias PMU="sudo pacman -Syu"
GRIM(){
    if [ "$#" -eq 1 ]; then
        grim -g "$(slurp)" ~/Downloads/screenshot-"$1"-"$(date +'%Y-%m-%dT%H:%M:%S').png"
    else
        grim -g "$(slurp)" ~/Downloads/screenshot"$(date +'%Y-%m-%dT%H:%M:%S').png"
    fi
}
alias H="history 0"
alias MW="mwinit -o && q whoami"
alias KI="kinit -f"
alias CL="card local --runtimePort 8082"
function ADA() {
    local account_number=$1
    if [ -z "$account_number" ]; then
        echo "Error: Please provide an account number"
        echo "Usage: ada_creds <account_number>"
        return 1
    fi
    ada credentials update --account="$account_number" --provider=conduit --role=IibsAdminAccess-DO-NOT-DELETE --once
}
function ADAL() {
    local account_number=$1
    if [ -z "$account_number" ]; then
        echo "Error: Please provide an account number"
        echo "Usage: ada_creds <account_number>"
        return 1
    fi
    ada credentials update --account="$account_number" --provider=conduit --role=IibsAdminAccess-DO-NOT-DELETE
}


# %% ---------- [syntax highlighting] ----------:

# should be last.
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh 2>/dev/null
